plugins {
    id("java-library")
    id("maven-publish")
    id("com.jfrog.bintray").version("1.8.5")
}

sourceCompatibility = 8
targetCompatibility = 8

project.group = "user11681"
project.version = "0.2.0"

repositories {
    mavenCentral()

    maven {url = "https://repository.ow2.org/nexus/content/repositories/snapshots"}
}

dependencies {
    implementation("it.unimi.dsi:fastutil:+")
    implementation("org.apache.logging.log4j:log4j-core:+")
    implementation("org.ow2.asm:asm:9.0-SNAPSHOT")
    implementation("org.ow2.asm:asm-tree:9.0-SNAPSHOT")

    testImplementation("org.junit.jupiter:junit-jupiter:+")
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")
    publications = ["bintray"]
    publish = true

    pkg {
        repo = "maven"
        name = project.name
        licenses = ["LGPL-3.0"]
        vcsUrl = "https://github.com/user11681/${project.name}.git"

        version {
            name = project.version
            released = new Date()
            githubRepo = "user11681/${project.name}"
            issueTrackerUrl = "https://github.com/user11681/${project.name}/issues"
        }
    }
}

task sourceJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set("sources")

    from(sourceSets.main.allSource)
}

artifacts {
    archives(sourceJar)
}

publishing {
    publications {
        bintray(MavenPublication) {
            groupId(project.group)
            artifactId(project.name)
            version(project.version)

            artifact(jar)
            artifact(sourceJar)
        }
    }
}
